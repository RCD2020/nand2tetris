// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zx  zero the x input
    Mux16(a=x, b[0..15]=false, sel=zx, out=xp1);

    // nx  negate the x input
    Not16(in=xp1, out=xp2);
    Mux16(a=xp1, b=xp2, sel=nx, out=xp3);


    // zy  zero the y input
    Mux16(a=y, b[0..15]=false, sel=zy, out=yp1);

    // ny  negate the y input
    Not16(in=yp1, out=yp2);
    Mux16(a=yp1, b=yp2, sel=ny, out=yp3);

    // f   + or &
    Add16(a=xp3, b=yp3, out=addxy);
    And16(a=xp3, b=yp3, out=xyand);
    Mux16(a=xyand, b=addxy, sel=f, out=outp1);

    // no  negate out (as well as ng)
    Not16(in=outp1, out=outp2);
    Mux16(a=outp1, b=outp2, sel=no, out=out, out[15]=ng, out[0..7]=zrp1, out[8..15]=zrp2);


    // zr
    Or8Way(in=zrp1, out=zrp3);
    Or8Way(in=zrp2, out=zrp4);
    Or(a=zrp3, b=zrp4, out=zr);    
}